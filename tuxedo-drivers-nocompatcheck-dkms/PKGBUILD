# Maintainer: Steven Seifried <gitlab@canox.net>
# Maintainer: LongSang01 <49708391+LongSang01@users.noreply.github.com>

_pkgname=tuxedo-drivers
pkgname=${_pkgname}-nocompatcheck-dkms
pkgver=4.15.3
pkgrel=1
pkgdesc="TUXEDO Computers kernel module drivers. Compatibility To MECHREVO"
url="https://gitlab.com/tuxedocomputers/development/packages/${_pkgname}"
license=("GPL-2.0-or-later")
arch=('x86_64')
depends=('dkms')
options=(!debug)
optdepends=('linux-headers: build modules against Arch kernel'
            'linux-lts-headers: build modules against LTS kernel'
            'linux-zen-headers: build modules against ZEN kernel'
            'linux-hardened-headers: build modules against the HARDENED kernel')
# tuxedo-keyboard-ite = ite_8291, ite_8291_lb, ite_8297 and ite_829x
provides=('tuxedo-drivers-dkms'
          'tuxedo-keyboard'
          'tuxedo-keyboard-ite'
          'tuxedo-io'
          'clevo-wmi'
          'clevo-acpi'
          'uniwill-wmi'
          'ite_8291'
          'ite_8291_lb'
          'ite_8297'
          'ite_829x')
conflicts=('tuxedo-drivers-dkms' 'tuxedo-keyboard-dkms' 'tuxedo-keyboard-ite-dkms')
source=($_pkgname-$pkgver.tar.gz::https://gitlab.com/tuxedocomputers/development/packages/tuxedo-drivers/-/archive/v$pkgver/$_pkgname-v$pkgver.tar.gz)
sha256sums=('SKIP')
sha512sums=('SKIP')

prepare() {
    extracted_dir=("${_pkgname}-v${pkgver}")
    cd "${extracted_dir}/src/tuxedo_compatibility_check"

    sed -i '/DMI_MATCH(DMI_SYS_VENDOR, "TUXEDO")/s/TUXEDO/MECHREVO/g' tuxedo_compatibility_check.c
    sed -i '/DMI_MATCH(DMI_BOARD_VENDOR, "TUXEDO")/s/TUXEDO/MECHREVO/g' tuxedo_compatibility_check.c
    sed -i '/DMI_MATCH(DMI_CHASSIS_VENDOR, "TUXEDO")/s/TUXEDO/MECHREVO/g' tuxedo_compatibility_check.c
}

package() {
    extracted_archive=("${_pkgname}-v${pkgver}")

    mkdir -p "${pkgdir}/usr/src/${_pkgname}-v${pkgver}"
    mkdir -p "${pkgdir}/etc/"
    mkdir -p "${pkgdir}/usr/lib/udev/hwdb.d"

    install -Dm644 "${extracted_archive}"/debian/tuxedo-drivers.dkms "${pkgdir}/usr/src/${_pkgname}-v${pkgver}/dkms.conf"
    sed -i "s/#MODULE_VERSION#/${pkgver}/" "${pkgdir}/usr/src/${_pkgname}-v${pkgver}/dkms.conf"

    cp -rav "${extracted_archive}"/etc/* -t "${pkgdir}/etc/"
    cp -rav "${extracted_archive}"/usr/* -t "${pkgdir}/usr"
    cp -rav "${extracted_archive}"/src/*  "${pkgdir}/usr/src/${_pkgname}-v${pkgver}"
}
